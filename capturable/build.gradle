plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    compileSdk 35

    defaultConfig {
        minSdk 21
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        buildConfig false
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion composeCompilerVersion
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    namespace 'dev.shreyaspatil.capturable'
}

dependencies {
    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    // Android core
    implementation "androidx.core:core-ktx:$androidCoreVersion"

    // Jetpack Compose
    def composeBom = platform("androidx.compose:compose-bom:$composeBomVersion")
    implementation composeBom
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.foundation:foundation"

    // Testing
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    androidTestImplementation composeBom
    androidTestImplementation "androidx.test.ext:junit:$androidJUnitTestVersion"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    androidTestImplementation "androidx.compose.ui:ui-test-manifest"

    // For using UI components in test
    androidTestImplementation "androidx.compose.material:material"
}

dokkaHtml.configure {
    outputDirectory.set(rootProject.mkdir("docs"))

    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

apply plugin: 'com.vanniktech.maven.publish'